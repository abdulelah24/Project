apply plugin: 'org.junit.gen5.gradle'

junit5 {
	// version '5.0.0-SNAPSHOT' // not needed since all dependencies are declared explicitly
	runJunit4 true
	matchClassName '.*Tests?'
	logManager 'org.apache.logging.log4j.jul.LogManager'
	// requireEngine 'junit5'
	// requireTag 'fast'
	// excludeTag 'slow'
}

def testedProjects = [
	project(':junit-commons'),
	project(':junit-console'),
	project(':junit-engine-api'),
	project(':junit-launcher'),
	project(':junit4-engine'),
	project(':junit4-runner'),
	project(':junit5-api'),
	project(':junit5-engine')
]

dependencies {
	testedProjects.each { project ->
		testCompile(project)
	}

	testCompile("org.assertj:assertj-core:${assertJVersion}")
	testCompile("org.mockito:mockito-core:${mockitoVersion}")

	testRuntime("org.apache.logging.log4j:log4j-core:${log4JVersion}")
	testRuntime("org.apache.logging.log4j:log4j-jul:${log4JVersion}")
}

if (project.hasProperty('enableClover')) {

	def cloverAggregationFile = "$cloverDir/coverage.db"

	task cloverAggregateDatabases(dependsOn: cloverTaskdef) {
		outputs.file cloverAggregationFile
		doLast {
			ant.'clover-merge'(initString: cloverAggregationFile) {
				testedProjects.each { project ->
					def cloverDbFile = project.file(project.cloverInitstring)
					if(cloverDbFile.exists()) {
						cloverDb(initString: project.cloverInitstring)
					}
				}
			}
		}
	}

	task cloverHtmlReport(dependsOn: cloverAggregateDatabases) {
		outputs.dir "$buildDir/reports/clover/html"
		doLast {
			ant."clover-report"(initString: cloverAggregationFile) {
				current(outfile: "$buildDir/reports/clover/html", title: rootProject.description, homepage: 'overview') {
					format(type: 'html')
				}
			}
		}
	}

	task cloverXmlReport(dependsOn: cloverAggregateDatabases) {
		outputs.file "$buildDir/reports/clover/clover.xml"
		doLast {
			ant."clover-report"(initString: cloverAggregationFile) {
				current(outfile: "$buildDir/reports/clover/clover.xml") {
					format(type: 'xml')
				}
			}
		}
	}

	afterEvaluate {
		cloverAggregateDatabases.dependsOn junit5Test
	}
}
