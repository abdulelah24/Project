description = rootProject.description + " (Bill of Materials)"

configurations.archives.artifacts.clear()
artifacts {
	archives file("README.md") // work around GRADLE-2406 by attaching text artifact
}

apply from: "$rootDir/gradle/maven.gradle"

uploadArchives {
	enabled = true
	repositories.mavenDeployer.pom {
		project {
			artifactId = project.name
			name = project.description
			description = project.description
		}
		whenConfigured {
			packaging = "pom"
			dependencies = []
			withXml {
				asNode().children().last() + {
					delegate.dependencyManagement {
						delegate.dependencies {
							parent.mavenizedProjects.sort()
								.findAll { name -> project.name != name }
								.collect { name -> rootProject.project(name) }
								.each { project ->
									delegate.dependency {
										delegate.groupId(project.group)
										delegate.artifactId(project.name)
										delegate.version(project.version)
									}
								}
						}
					}
				}
			}
		}
	}
}
