plugins {
	id "me.champeau.gradle.jmh" version "0.4.5"
}

apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
	filters {
		engines {
			include 'junit-jupiter'
		}
		includeClassNamePattern '.*Tests?'
	}
	logManager 'org.apache.logging.log4j.jul.LogManager'
}

// Tests in `AutomaticModuleNameTests` depend on all and the three jars. #894
junitPlatformTest.dependsOn(
	project(':junit-jupiter-migrationsupport').jar,
	project(':junit-platform-gradle-plugin').jar,
	project(':junit-platform-surefire-provider').jar
)

junitPlatformTest.systemProperty 'coverage.enabled', project.hasProperty('enableClover') ? 'true' : 'false'

dependencies {
	// --- Things we are testing --------------------------------------------------
	testImplementation(project(':junit-platform-commons'))
	testImplementation(project(path: ':junit-platform-console', configuration: 'shadow'))
	testImplementation(project(':junit-platform-engine'))
	testImplementation(project(':junit-platform-launcher'))

	// --- Things we are testing with ---------------------------------------------
	testImplementation(project(':junit-jupiter-api'))
	testImplementation(project(':junit-jupiter-params'))
	testImplementation(project(':junit-platform-runner'))
	testImplementation(project(path: ':junit-platform-engine', configuration: 'testArtifacts'))
	testImplementation(project(path: ':junit-jupiter-engine', configuration: 'testArtifacts'))
	testImplementation("org.apiguardian:apiguardian-api:${apiGuardianVersion}")
	testImplementation("org.assertj:assertj-core:${assertJVersion}")
	testImplementation("org.mockito:mockito-core:${mockitoVersion}")
	testImplementation("io.github.lukehutch:fast-classpath-scanner:2.11.0")

	// --- Test run-time dependencies ---------------------------------------------
	testRuntimeOnly(project(':junit-jupiter-engine'))
	testRuntimeOnly(project(':junit-vintage-engine'))
	testRuntimeOnly(project(':junit-jupiter-migrationsupport')) // included for @API report
	testRuntimeOnly(project(':junit-platform-gradle-plugin')) // included for @API report
	testRuntimeOnly(project(':junit-platform-surefire-provider')) // included for @API report
	testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	testRuntimeOnly("org.apache.logging.log4j:log4j-jul:${log4jVersion}")

	// --- http://openjdk.java.net/projects/code-tools/jmh/ -----------------------
	jmh('org.openjdk.jmh:jmh-core:1.20')
	jmh('org.openjdk.jmh:jmh-generator-annprocess:1.20')
	jmh(project(':junit-jupiter-api'))
}

jmh {
	jmhVersion = '1.20'

	duplicateClassesStrategy = 'warn'
	fork = 0 //instead of fork = 1 (Too long command line on Windows...)
	iterations = 5
	warmupIterations = 1
	warmupBatchSize = 1
}

checkstyleJmh { // use same style rules as defined for tests
	configFile = rootProject.file('src/checkstyle/checkstyleTest.xml')
}
